pkgname=nsgallery-git
pkgver=0.1
pkgrel=1
pkgdesc="A naively simple web gallery"
arch=('x86_64')
url="https://github.com/MetroWind/nsgallery"
license=('WTFPL')
groups=()
depends=('imagemagick' 'perl-image-exiftool' 'libheif' 'libwebp' 'svt-av1')
makedepends=('git' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=()
backup=("etc/nsgallery.yaml")
options=()
install=
source=('git+https://github.com/MetroWind/nsgallery.git' "sysusers-nsgallery.conf" "nsgallery.service" "nsgallery.yaml")
noextract=()
sha256sums=('SKIP' "6d5bb86ad5a5beec9ab933f5c1ff60f42073d31ef0fe6fd722ebe4ed8c1d37b3"
         "9c0577c9e4be92dbe0830cb8abd1152e3be1e8507a56a5ae85fae7167b8f3686"
         "17bb6b0fecdee541dcacdfb6c62d6baefdf5aa5a51f6b79e2b2df6500406e724")

pkgver()
{
	cd "$srcdir/${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build()
{
	cd "$srcdir/${pkgname%-git}"
    cmake -B build \
          -DCMAKE_BUILD_TYPE='None' \
          -DCMAKE_INSTALL_PREFIX='/usr' \
          -Wno-dev .
    cmake --build build
}

package()
{
    install -Dm755 -t "$pkgdir/usr/bin" "${srcdir}/${pkgname%-git}/build/nsgallery"
    mkdir -pv "$pkgdir/var/lib/nsgallery"
    cp -r "${srcdir}/${pkgname%-git}/statics" "${srcdir}/${pkgname%-git}/templates" "${pkgdir}/var/lib/nsgallery"
    install -Dm644 -t "$pkgdir/etc" "${srcdir}/nsgallery.yaml"
    install -Dm644 "${srcdir}/sysusers-nsgallery.conf" "${pkgdir}/usr/lib/sysusers.d/nsgallery.conf"
    install -Dm644 "${srcdir}/nsgallery.service" "${pkgdir}/usr/lib/systemd/system/nsgallery.service"
}
